@description('The name of the API Management resource to be created.')
param apimName string

@description('The email address of the publisher of the APIM resource.')
@minLength(1)
param publisherEmail string = 'apim@contoso.com'

@description('Company name of the publisher of the APIM resource.')
@minLength(1)
param publisherName string = 'Contoso'

@description('The pricing tier of the APIM resource.')
param skuName string = 'Developer'

@description('The instance size of the APIM resource.')
param capacity int = 1

@description('Location for Azure resources.')
param location string = resourceGroup().location

param appInsightsName string

/*
 * Resources
*/
resource appInsights 'Microsoft.Insights/components@2020-02-02-preview' existing = {
  name: appInsightsName
}

resource apimResource 'Microsoft.ApiManagement/service@2020-12-01' = {
  name: apimName
  location: location
  sku: {
    capacity: capacity
    name: skuName
  }
  properties: {
    virtualNetworkType: 'External'
    publisherEmail: publisherEmail
    publisherName: publisherName
  }
}

resource apimPolicy 'Microsoft.ApiManagement/service/policies@2021-12-01-preview' = {
  name: 'policy'
  parent: apimResource
  properties: {
    format: 'rawxml'
    value: loadTextContent('apimPolicies/global.xml')
  }
}

resource appInsightsLogger 'Microsoft.ApiManagement/service/loggers@2019-01-01' = {
  parent: apimResource
  name: appInsightsName
  properties: {
    loggerType: 'applicationInsights'
    resourceId: appInsights.id
    credentials: {
      instrumentationKey: appInsights.properties.InstrumentationKey
    }
  }
}

resource appInsightsDiagnostics 'Microsoft.ApiManagement/service/diagnostics@2019-01-01' = {
  parent: apimResource
  name: 'applicationinsights'
  properties: {
    loggerId: appInsightsLogger.id
    alwaysLog: 'allErrors'
    sampling: {
      percentage: 100
      samplingType: 'fixed'
    }
  }
}

module accounting 'apim.accounting.bicep' = {
  name: '${apimName}--accounting'
  dependsOn: [
    apimResource
  ]
  params: {
    apimName: apimName
  }
}

module makeline 'apim.makeline.bicep' = {
  name: '${apimName}--makeline'
  dependsOn: [
    apimResource
  ]
  params: {
    apimName: apimName
  }
}

module order 'apim.order.bicep' = {
  name: '${apimName}--order'
  dependsOn: [
    apimResource
  ]
  params: {
    apimName: apimName
  }
}

output gatewayUrl string = apimResource.properties.gatewayUrl
output portalUrl string = apimResource.properties.portalUrl
